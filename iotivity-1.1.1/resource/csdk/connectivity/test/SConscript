#******************************************************************
#
# Copyright 2015 Samsung Electronics All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

import os
import os.path

# SConscript file for Local PKI google tests
gtest_env = SConscript('#extlibs/gtest/SConscript')
catest_env = gtest_env.Clone()

src_dir = catest_env.get('SRC_DIR')
target_os = catest_env.get('TARGET_OS')
target_transport = catest_env.get('TARGET_TRANSPORT')

######################################################################
# Build flags
######################################################################
catest_env.PrependUnique(CPPPATH = [
                '../../ocsocket/include',
                '../../logger/include',
                '../../stack/include',
                '../../extlibs/cjson',
                '../../../oc_logger/include',
               ])

tmplist = catest_env['LINKFLAGS'][:]
if '-lpthread' in tmplist:
    tmplist.remove('-lpthread')
    catest_env.Replace(LINKFLAGS = tmplist)

if target_os not in ['windows']:
	catest_env.AppendUnique(CXXFLAGS = ['-std=c++0x', '-Wall', '-pthread'])
catest_env.AppendUnique(LIBPATH = [catest_env.get('BUILD_DIR')])
catest_env.PrependUnique(LIBS = ['gtest', 'gtest_main'])
catest_env.PrependUnique(LIBS = ['octbstack',
                                    'connectivity_abstraction',
                                    'coap'])

if target_os not in ['arduino', 'darwin', 'ios']:
	catest_env.AppendUnique(LIBS=['rt'])

if catest_env.get('SECURED') == '1':
	catest_env.AppendUnique(LIBS = ['tinydtls'])

if catest_env.get('LOGGING'):
	catest_env.AppendUnique(CPPDEFINES = ['TB_LOG'])

if target_os not in ['msys_nt', 'windows']:
	catest_env.AppendUnique(LIBS = ['m'])
	catest_env.PrependUnique(LIBS = ['pthread'])

######################################################################
# Source files and Targets
######################################################################
catests = catest_env.Program('catests', ['catests.cpp',
                                         'caprotocolmessagetest.cpp',
                                               'ca_api_unittest.cpp',
                                               'camutex_tests.cpp',
                                               'uarraylist_test.cpp'
                                               ])

Alias("test", [catests])

catest_env.AppendTarget('test')
if catest_env.get('TEST') == '1':
        if target_os in ['linux']:
                catest_env.AppendENVPath('PATH', catest_env.get('BUILD_DIR'))
                from tools.scons.RunTest import *
                run_test(catest_env,
                         'resource_csdk_connectivity_test.memcheck',
                         'resource/csdk/connectivity/test/catests')
